?start: expr
        | linear_expr

?expr: infix_expr
     | prefix_expr
     | postfix_expr

//INFIX notation
?infix_expr: infix_expr "+" infix_term   -> add
           | infix_expr "-" infix_term   -> sub
           | infix_term
           | "inv" "(" infix_expr ")"    -> inverse
           | "transpose" "(" infix_expr ")" -> transpose
           | "(" "-" infix_expr ")"      -> neg
           | "log" "(" infix_expr ")"  -> log
           | "ln" "(" infix_expr ")"  -> ln
           | "sin" "(" infix_expr ")"  -> sin
           | "cos" "(" infix_expr ")"  -> cos
           | "tan" "(" infix_expr ")"  -> tan
           | "arcsin" "(" infix_expr ")"  -> arcsin
           | "arccos" "(" infix_expr ")"  -> arccos
           | "arctan" "(" infix_expr ")"  -> arctan
           | "sinh" "(" infix_expr ")"  -> sinh
           | "cosh" "(" infix_expr ")"  -> cosh
           | "tanh" "(" infix_expr ")"  -> tanh
           | "rand" "(" infix_expr ")"   -> rand


?infix_term: infix_term "*" infix_factor -> mul
           | infix_term "/" infix_factor -> div
           | infix_factor


?infix_factor: factor
             | infix_factor "^" infix_factor -> pow
             | infix_factor "nroot" infix_factor -> nroot
             | "(" "-" factor ")"                    -> neg
             | "(" infix_expr ")"

//PREFIX notation
?prefix_expr: "+" "(" prefix_term (","? prefix_term)+ ")"   -> add
            | "-" "(" prefix_term (","? prefix_term)+ ")"   -> sub
            | "*" "(" prefix_term (","? prefix_term)+ ")"   -> mul
            | "/" "(" prefix_term (","? prefix_term)+ ")"   -> div
            | "^" "(" prefix_term (","? prefix_term)+ ")"   -> pow
            | "nroot" "(" prefix_term (","? prefix_term)+ ")"   -> nroot
            | "inv" "(" prefix_expr ")"                     -> inverse
            | "transpose" "(" prefix_expr ")"               -> transpose
            | "(" "-" prefix_expr ")"                      -> neg
            | "log" "(" prefix_expr ")"                          -> log
            | "ln" "(" prefix_expr ")"                           -> ln
            | "sin" "(" prefix_expr ")"                           -> sin
            | "cos" "(" prefix_expr ")"                           -> cos
            | "tan" "(" prefix_expr ")"                           -> tan
            | "arcsin" "(" prefix_expr ")"                       -> arcsin
            | "arccos" "(" prefix_expr ")"                        -> arccos
            | "arctan" "(" prefix_expr ")"                        -> arctan
            | "sinh" "(" prefix_expr ")"                         -> sinh
            | "cosh" "(" prefix_expr ")"                         -> cosh
            | "tanh" "(" prefix_expr ")"                         -> tanh
            | "rand" "(" prefix_expr ")"                                   -> rand

?prefix_term: prefix_expr
            | prefix_factor

?prefix_factor: factor
              | "(" "-" factor ")"    -> neg

//POSTFIX notation
?postfix_expr: "(" postfix_term (","? postfix_term)+ ")" "+"   -> add
             | "(" postfix_term (","? postfix_term)+ ")" "-"   -> sub
             | "(" postfix_term (","? postfix_term)+ ")" "*"   -> mul
             | "(" postfix_term (","? postfix_term)+ ")" "/"   -> div
             | "(" postfix_term (","? postfix_term)+ ")" "^"   -> pow
             | "(" postfix_term (","? postfix_term)+ ")" "nroot"   -> nroot
             | "inv" "(" postfix_expr ")"                      -> inverse
             | "transpose" "(" postfix_expr ")"               -> transpose
             | "(" postfix_expr "-" ")"                       -> neg
             |  "(" postfix_expr ")" "log"                      -> log
             |  "(" postfix_expr ")" "ln"                       -> ln
             |  "(" postfix_expr ")" "sin"                      -> sin
             |  "(" postfix_expr ")" "cos"                      -> cos
             |  "(" postfix_expr ")" "tan"                      -> tan
             |  "(" postfix_expr ")" "arcsin"                   -> arcsin
             |  "(" postfix_expr ")" "arccos"                   -> arccos
             |  "(" postfix_expr ")" "arctan"                   -> arctan
             |  "(" postfix_expr ")" "sinh"                     -> sinh
             |  "(" postfix_expr ")" "cosh"                     -> cosh
             |  "(" postfix_expr ")" "tanh"                     -> tanh
             |  "(" postfix_expr ")" "rand"                                  -> rand


?postfix_term: postfix_expr
             | postfix_factor

?postfix_factor: factor
               | "(" factor "-" ")" -> neg


linear_expr: "solve_linear" "(" STRING ")"

STRING: /"[^"]*"/

//FACTOR
?factor: INT           -> integer_number
       | DECIMAL       -> real_number
       | factor "j"    -> complex_number
       | matrix        -> matrix
       | "inv" "(" matrix ")" -> matrix_inverse
       | "e"           -> cst_e
       | "pi"          -> cst_pi

?matrix: "[" row ("," row)* "]"
?row: "[" factor ("," factor)* "]"


%import common.INT
%import common.DECIMAL
%import common.WS_INLINE
%ignore WS_INLINE